// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer).
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        let powersOfTwo = Array.new(16);
        let powersOfTwo[0] = 1;
        let powersOfTwo[1] = 2;
        let powersOfTwo[2] = 4;
        let powersOfTwo[3] = 8;
        let powersOfTwo[4] = 16;
        let powersOfTwo[5] = 32;
        let powersOfTwo[6] = 64;
        let powersOfTwo[7] = 128;
        let powersOfTwo[8] = 256;
        let powersOfTwo[9] = 512;
        let powersOfTwo[10] = 1024;
        let powersOfTwo[11] = 2048;
        let powersOfTwo[12] = 4096;
        let powersOfTwo[13] = 8192;
        let powersOfTwo[14] = 16384;
        let powersOfTwo[15] = 16384 + 16384;
        return;
    }

    //Returns true if the j-th bit of the integer x is 1 and false otherwise
    function boolean bit(int x, int j){
        return x & powersOfTwo[j];
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method.
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, index, mask;
        if ((x=0) | (y=0)){
            return 0;
        }
        if (x = 1){
            return y;
        }
        if (y = 1){
            return x;
        }
        if (x = 2){
            return y + y;
        }
        if (y = 2){
            return x+x;
        }

        let mask = 1;
        while (index < 16) {
            if (y & mask) {
                let sum = sum + x;
            }
            let x = x + x;
            let index = index + 1;
            let mask = mask + mask;
        }

        return sum;
    }


    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int q; // Quotient
        var int y_a;
        var int x_a;
        let x_a = Math.abs(x);
        let y_a = Math.abs(y);
        if ((x = 0) | (y_a > x_a)) {
            return 0;
        }
        if (x = y) {
            return 1;
        }
        if ((x < 0) | (y < 0)) {
            let q = x_a / y_a;
             if ((x < 0) & (y < 0)){
                return q;
             }
            return -q;
        }
        let q = x / (y + y);
        if ((x - ((q + q) * y)) < y) {
            return q + q;
        }
        return q + q + 1;
    }


   /** Returns the remainder part of x/y */
    function int mod(int x, int y) {
        return x - ((x / y) * y);
    }



    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, k, k_squared;
        let y = 0;
        let j = 7;
        while (j >= 0){
            let k = y + powersOfTwo[j]; //k = y + 2^j
            let k_squared = k * k;
            if ((k_squared <= x) & (k_squared > 0)){
                let y = k;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a > b){return a;}
        return b;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b){return a;}
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        return Math.max(x , -x);
    }
}

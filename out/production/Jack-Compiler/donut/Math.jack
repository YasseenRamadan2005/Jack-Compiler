// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer).
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int mask, i;
        let mask = 1;
        let i = 0;
        let powersOfTwo = Array.new(16);
        while (i < 16){
           let powersOfTwo[i] = mask;
           let mask=mask+mask;
           let i = i + 1;
        }
        return;
    }

    //Returns true if the j-th bit of the integer x is 1 and false otherwise
    function boolean bit(int x, int j){
        return x & powersOfTwo[j];
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method.
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, index, mask;
        if ((x=0) | (y=0)){
            return 0;
        }
        if (x = 1){
            return y;
        }
        if (y = 1){
            return x;
        }
        if (x = 2){
            return y + y;
        }
        if (y = 2){
            return x+x;
        }

        let mask = 1;
        let index = 0;
        let sum = 0;
        while (index < 16) {
            if ((y & mask) ~= 0) {
                let sum = sum + x;
            }
            let x = x + x;
            let index = index + 1;
            let mask = mask + mask;
        }

        return sum;
    }




    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
        function int divide(int x, int y) {
          var int q, nx, ny;

          let nx = Math.abs(x);
          let ny = Math.abs(y);

          if (y = 0){
            return 0;
          }
          if (ny > nx) {
            return 0;
          }

          //Overflow case
          if ((ny + ny )< 0){
            return 1;
          }
          let q = Math.divide(nx, ny + ny);
          if ((nx - ((q+q) * ny)) < ny) {
            if (((x < 0) & (y > 0)) | ((x > 0) & (y < 0))) {
              let q = -q;
            }
            return q + q;
          } else {
            if (((x < 0) & (y > 0)) | ((x > 0 )& (y < 0))) {
              let q = -q;
            }
            return (q + q) + 1;
          }
        }



   /** Returns the remainder part of x/y */
    function int mod(int x, int y) {
        return x - ((x / y) * y);
    }

    //Returns 2 ^ x
    function int twoToThe(int x){
        return powersOfTwo[x];
    }


    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, k, k_squared;
        let y = 0;
        let j = 7;
        while (j >= 0){
            let k = y + powersOfTwo[j]; //k = y + 2^j
            let k_squared = k * k;
            if ((k_squared <= x) & (k_squared > 0)){
                let y = k;
            }
            let j = j - 1;
        }
        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a > b){return a;}
        return b;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b){return a;}
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        return Math.max(x , -x);
    }

    // Bit 14 = sign, 13-7 = exponent, 6-0 = fraction
    function float multiply_floats(int a, int b) {
        var int signA, signB, signR;
        var int expA, expB, fracA, fracB;
        var int mantA, mantB, productMant;
        var int expShift, fracR;
        var int unbiasedA, unbiasedB, unbiasedR, expR;

        // --- Extract signs ---
        let signA = a < 0;
        let signB = b < 0;
        let signR = (signA & ~signB) | (signB & ~signA);
        if (signR = -1) { let signR = 1; } else { let signR = 0; }

        // --- Extract exponent and fraction ---
        let expA = (a / 128) & 127;
        let fracA = a & 127;
        let expB = (b / 128) & 127;
        let fracB = b & 127;

        // --- Handle special cases ---
        if (expA = 127) {
            if (fracA ~= 0) { return (signR * 16384) | (127 * 128) | 1; } // NaN
            else { return (signR * 16384) | (127 * 128); } // Inf
        }
        if (expB = 127) {
            if (fracB ~= 0) { return (signR * 16384) | (127 * 128) | 1; } // NaN
            else { return (signR * 16384) | (127 * 128); } // Inf
        }
        if (((expA = 0) & (fracA = 0)) | ((expB = 0) & (fracB = 0))) { return 0; } // zero

        // --- Compute mantissas ---
        if (expA = 0) { let mantA = fracA; } else { let mantA = 128 + fracA; }
        if (expB = 0) { let mantB = fracB; } else { let mantB = 128 + fracB; }

        // --- Multiply mantissas ---
        let productMant = mantA * mantB;
        let expShift = 0;

        // --- Normalize productMant to have leading 1 in bit 7 ---
        while (productMant < 128) {
            let productMant = productMant * 2;
            let expShift = expShift - 1;
        }
        while (productMant >= 256) {
            let productMant = productMant / 2;
            let expShift = expShift + 1;
        }

        // --- Extract 7-bit fraction (drop leading 1) ---
        let fracR = productMant & 127;

        // --- Compute unbiased exponents ---
        if (expA = 0) { let unbiasedA = -62; } else { let unbiasedA = expA - 63; }
        if (expB = 0) { let unbiasedB = -62; } else { let unbiasedB = expB - 63; }

        // --- Fix: subtract 7 for the 128-scaling of productMant ---
        let unbiasedR = unbiasedA + unbiasedB + expShift - 7;
        let expR = unbiasedR + 63;

        // --- Handle overflow/underflow ---
        if (expR >= 127) { return (signR * 16384) | (127 * 128); } // Inf
        if (expR <= 0) { return 0; } // zero/subnormal not handled separately

        // --- Assemble result ---
        return (signR * 16384) | (expR * 128) | fracR;
    }


    function int divide_floats(int a, int b) {
    }

}
